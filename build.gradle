import java.util.jar.JarFile

static def getJenkinsBuild() {
    def buildNumber = System.getenv().BUILD_NUMBER?.toInteger()
    return buildNumber ?: 0
}

ext.jenkinsBuildNumber = getJenkinsBuild()

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'org.yunshanmc'
    version = "$pluginVersion.${rootProject.ext.jenkinsBuildNumber}-$pluginVersionType"

    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs = ['-parameters', '-Xlint:deprecation']
    }

    javadoc {
        options.encoding "UTF-8"
        options.charSet 'UTF-8'
        options.links("http://docs.oracle.com/javase/8/docs/api/")
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    jar {
        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }

    repositories {
        maven {
            name 'Aliyun Maven Center'
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        maven {
            name 'Aliyun JCenter'
            url 'http://maven.aliyun.com/nexus/content/repositories/jcenter'
        }
        maven {
            name 'SpigotMC'
            url 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }
    }

    dependencies {
        compileOnly 'com.google.guava:guava:22.0'
        compileOnly 'org.yaml:snakeyaml:1.20'
        // ORM
        compile 'com.j256.ormlite:ormlite-core:5.1'
        compile 'com.j256.ormlite:ormlite-jdbc:5.1'
        // CGLIB
        compile('cglib:cglib:3.2.6') {
            exclude group: 'org.apache.ant', module: 'ant'
        }

        testCompile 'junit:junit:4.12'
        testCompile 'org.yaml:snakeyaml:1.18'
        testCompile 'com.google.guava:guava:22.0'
    }

    publishing {
        publications {
            Plugin(MavenPublication) {
                groupId project.group
                artifactId archivesBaseName
                version project.version

                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }

}