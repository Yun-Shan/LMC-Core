import java.util.jar.JarFile

static def getJenkinsBuild() {
    def buildNumber = System.getenv().BUILD_NUMBER?.toInteger()
    return buildNumber ?: 0
}

ext.jenkinsBuildNumber = getJenkinsBuild()

subprojects {
    apply plugin: 'java'

    group = 'org.yunshanmc'
    version = "$pluginVersion.${rootProject.ext.jenkinsBuildNumber}-$pluginVersionType"

    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs = ['-parameters', '-Xlint:deprecation']
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    jar {
        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }

    repositories {
        maven {
            name 'Aliyun Maven Center'
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
        maven {
            name 'Aliyun JCenter'
            url 'http://maven.aliyun.com/nexus/content/repositories/jcenter'
        }
        maven {
            name 'SpigotMC'
            url 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }
        maven {
            name 'For PlaceholderAPI'
            url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
        }
    }

    dependencies {
        compileOnly 'com.google.guava:guava:22.0'
        compileOnly 'org.yaml:snakeyaml:1.18'
        // ORM
        compile 'com.j256.ormlite:ormlite-core:5.0'
        compile 'com.j256.ormlite:ormlite-jdbc:5.0'
        // CGLIB
        compile ('cglib:cglib:3.2.6') {
            exclude group: 'org.apache.ant', module: 'ant'
        }

        testCompile 'junit:junit:4.12'
        testCompile 'org.yaml:snakeyaml:1.18'
        testCompile 'com.google.guava:guava:22.0'
    }
}